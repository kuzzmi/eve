#!/usr/bin/env coffee

readline = require 'readline'
colors   = require 'colors'
request  = require 'request'

argv     = require('minimist')(process.argv.slice(2))

{ Speech, Client, Config } = require '..'

process.stdout.write '\u001B[2J\u001B[0;0f'

class CLI extends Client
    run: ->        
        @rl = readline.createInterface 
            input  : process.stdin
            output : process.stdout
        
        @rl.on 'line', (data) => @send data

        @rl.setPrompt 'Igor: '
        @rl.prompt true

    stop: ->
        super
        @rl.close()

    send: (msg) ->
        super msg
        @rl.prompt true

    print: (data) ->
        data = data.join '. ' if data instanceof Array

        process.stdout.clearLine();
        process.stdout.cursorTo(0);
        console.log 'Eve: '.bold.cyan + data

        @rl.prompt true

    say: (phrase) ->
        phrase = phrase.join '. ' if phrase instanceof Array
        Speech.exec phrase

    notify: (notification) ->
        url = Config.autoremote.lgg2
        
        @Logger.debug notification

        if notification instanceof Array and notification.length > 1
            command = 'eve_resp_list'
            message = notification.join(',') + '=:=' + command
        else 
            command = 'eve_resp_text'
            message = notification.join('. ') + '=:=' + command

        request url + message

host      = argv.h || argv.host
port      = argv.p || argv.port
verbosity = if argv.v then 'debug'

CLI.create(host, port, verbosity)